package io.franzbecker.gradle.lombok.task

import io.franzbecker.gradle.lombok.AbstractIntegrationTest

/**
 * Integration tests for {@link DelombokTask}.
 */
class DelombokTaskIntegrationTest extends AbstractIntegrationTest {

    def "Calling delombok with --help throws no exception."() {
        given:
        buildFile << """
            import io.franzbecker.gradle.lombok.task.DelombokTask

            task delombokHelp(type: DelombokTask) {
                args "--help"
            }
        """.stripIndent()

        when:
        runBuild("delombokHelp")

        then:
        noExceptionThrown()
    }

    def "Delombok on directory produced proper output"() {
        given:
        createSourceForDelombok()

        and:
        buildFile << """
            import io.franzbecker.gradle.lombok.task.DelombokTask

            dependencies {
                compile 'org.slf4j:slf4j-api:1.7.12'
            }

            task delombok(type: DelombokTask) {
                args("src/main/java", "-d", "src/gen/java")

                // Redirect the output of the process
                standardOutput = new ByteArrayOutputStream()
                errorOutput = new ByteArrayOutputStream()
                doLast {
                    // Fail the build if there was any error output
                    if (!errorOutput.toString().isEmpty()) {
                        throw new GradleException(errorOutput.toString())
                    }
                }
            }
        """.stripIndent()

        when:
        runBuild("delombok")

        then:
        File file = new File(projectDir, "src/gen/java/com/example/HelloWorld.java")
        file.exists()
        String contents = file.text
        !contents.contains("import lombok.Data")
        contents.contains("// Generated by delombok")
        contents.contains("public String getId() {")
    }

    private void createSourceForDelombok() {
        def file = createFile("src/main/java/com/example/HelloWorld.java")
        file << """
            package com.example;

            import lombok.Data;
            import org.slf4j.Logger;
            import org.slf4j.LoggerFactory;

            @Data
            public class HelloWorld {

                private static final Logger logger = LoggerFactory.getLogger(HelloWorld.class);

                private String id;

            }
        """.stripIndent()
    }

}
